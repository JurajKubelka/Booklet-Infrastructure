!! Commander: A command pattern library

In this chapter you will learn about a new library to manage commands. The library is based on three main objects: ''commands'' that represent the actions to be executed, ''command activation strategies'' that represent ways commands are activated (i.e., using shortcuts, menus..) and ''contexts'' which represent the state of application, users of commands.

!!! Commands

Commander models application actions as first class objects following the Command design pattern.
Every action is implemented as separate command class (subclass of ==CmdCommand==) with the ==execute== method and all state required for execution.

!!! An example

Let's look how we can apply commands to control simple contact book.
The code of example is packaged inside Commander-Examples package. So you can load it and play.

In our domain we need two classes one for the contact and one for the contact book.

!!!! 1. Contact with name and phone

[[[
Object subclass: #Contact
	instanceVariableNames: 'name phone'
	classVariableNames: ''
	package: 'Commander-Examples'
]]]

[[[
Contact >> printOn: aStream
	super printOn: aStream.

	aStream nextPut: $(.
	aStream nextPutAll: name.
	aStream nextPut: $).
]]]

!!!! 2. Contact book: a collection of contacts

[[[
Object subclass: #ContactBook
	instanceVariableNames: 'contents'
	classVariableNames: ''
	package: 'Commander-Examples'
]]]

[[[
ContactBook >> addContact: aContact
	contents add: aContact
]]]

[[[
ContactBook >> add: contactName phone: phone
	| contact |
	contact := CmdContact named: contactName phone: phone.
	self addContact: contact.
	^contact
]]]

[[[
ContactBook >> includesContact: aContact
	^ contents includes: aContact
]]]

!!!! 3. A first command
Now we can create ==RenameContactCommand== to rename given contact

[[[
CmdCommand subclass: #RenameContactCommand
	instanceVariableNames: 'contact newName'
	classVariableNames: ''
	package: 'Commander-Examples'
]]]

[[[
RenameContactCommand >> execute
	contact name: newName
]]]

We need accessors and constructors for all these classes. For example for the command it would be:

[[[
RenameContactCommand class >> contact: aContact to: newNameString
	^self new
			contact: aContact;
			newName: newNameString
]]]

And in playground we are able rename contact using command:

[[[
| contact command |
contact := Contact new.
command := RenameContactCommand contact: contact newName: 'John Smith'.
command execute.
]]]

!!! Adding a simple UI

Before we continue we should write simple UI which will show contact book contents.
You should be fimilar with Spec UI framework to follow this example.

We need just one presenter which will show contact table

[[[
ComposablePresenter subclass: #ContactBookPresenter
	instanceVariableNames: 'table contactBook'
	classVariableNames: ''
	package: 'Commander-Examples'
]]]

[[[
ContactBookPresenter >> initializeWidgets
	table := self newList.
	table numberOfColumns: 2 withWidths: #(250 250).
	table displayBlock: [ :contact | {contact name. contact phone} ].
	table items: contactBook contents
]]]

Also let's define a reusable book as class side variable

[[[
ContactBook >> family
	^family ifNil: [
		family := self new
			add: 'John' phone: '342 345';
			add: 'Bill' phone: '123 678';
			add: 'Marry' phone: '789 567';
			yourself ]
]]]

Now we are able show family phone book with Spec UI:

[[[
ContactBookPresenter class >> familyExample
	<example>
	^ (self on: ContactBook family) openWithSpec
]]]

We put it as example. So you can run it from the browser using suitable icon:

%+Family book window.>file://Figures/Family.png+
%+Family book window.>file://figures/Family.png|label=Family+

!!! Activation strategy

Commands are reusable objects and applications provide various ways to access them: shortcuts, context menu, buttons, etc.
This information is attached to command classes as activation strategies. Currently there are three types of activations:
- ==CmdShortcutCommandActivation==
- ==CmdContextMenuCommandActivation==
- ==CmdDragAndDropCommandActivation==

Strategies annotate command as contextual class annotations. Look at the project ClassAnnotation for details *https://github.com/dionisiydk/ClassAnnotation*.

Strategies are always attached to commands with concrete application context where they can be used.
In the simplest case, it can be just a class of user application or widget like the class ==ContactBookPresenter==.

For example the following method allows an instance of ==RenameContactCommand== to be executed by shortcut in the context of our presenter:

[[[
RenameContactCommand class >> shortcutActivation
	<classAnnotation>
	^ CmdShortcutCommandActivation by: $r meta for: ContactBookPresenter
]]]

Similarly the following method ==menuActivation== defines that the ==RenameContactCommand== can be executed from a context menu. Note the use of ==CmdContextMenuCommandActivation== in the method, while the previous one was using ==CmdShortcutCommandActivation==.

[[[
RenameContactCommand class >> menuActivation
	<classAnnotation>
	^CmdContextMenuCommandActivation byRootGroupItemFor: ContactBookPresenter
]]]


!!!! Set the presenter

Now when you open window neither shortcuts or context menu will work. The problem is that Spec has no information about Commander. Spec only provides basic functionality to define menu and shortcuts on low level. So you should manually activate Commander in your Spec presenter.

Modify ==initializeWidgets== method to activate context menu:

[[[
ContactBookPresenter >> initializeWidgets
	table := self newList.
	table numberOfColumns: 2 withWidths: #(250 250).
	table displayBlock: [ :contact | {contact name . contact phone} ].
	table items: contactBook contents
	table enableCommanderMenuBy: self
]]]

===enableCommanderMenuBy:=== adds context menu to table presenter which will be filled by commands defined in context of presenter in the argument (==ContactBookPresenter==).

In this case the right click on table will show the menu with single item named ==RenameContactCommand==.
To specify better name for menu, you need to define the method ==defaultMenuItemName==:

[[[
RenameContactCommand >> defaultMenuItemName
	^'Rename'
]]]

!!! Getting the information from the user

Now if you will click on 'Rename' menu item you will get an exception. From the debugger you can investigate that command instance is not initialized: ==contact== and ==newName== are nil.
Commander provides method where command should retrieve required state from given context instance:

[[[
RenameContactCommand >> prepareExecutionInContext: aToolContext
	super prepareExecutionInContext: aToolContext.

	contact := aToolContext selectedContact.

	newName := UIManager default
		request: 'New name of the contact'
		initialAnswer: contact name
		title: 'Rename a contact'.
	newName isEmptyOrNil | (newName = contact name) ifTrue: [ ^ CmdCommandAborted signal ]
]]]

By default (with help of Spec integration) Commander will pass presenter instance to aToolContext argument. But in general it is not enough and you will see example later.

Now you only need to know that context represents application state and in ==prepare== method we define what information is required to be able reuse commands in various applications.

In our example we expect to receive ==selectedContact== from the context. And we request ==newName== directly from user.
Notice that Commander provides way how to define specific preparation method for specific application context. By default it is ==prepareExecutionInContext:== but it can be different for advanced scenarios.

So we need ==selectedContact== for presenter to make it compatible to context of our command:

[[[
ContactBookPresenter >> selectedContact
	^ table selectedItem
]]]

Now if you will click on 'Rename' item you will be requested for new contact name. But after submit the view will be not updated. Reopen presenter window to see the result.

!!!! Refreshing the view

It is your responsibility to manage updates between application and model. You can use some event mechanism which will automatically udpate view after modification of model.

But Commander provides very simple way to apply result of command which can be also used to update the view.

==applyResultInContext:== is called when command is executed. You can implement it on your commands to produce desired effect in context of your application:

[[[
RenameContactCommand>>applyResultInContext: aToolContext
	super applyResultInContext: aToolContext.

	aToolContext updateView
]]]

Here we require that context should be able update the view. And presenter should support it:

[[[
ContactBookPresenter >> updateView
	table items: contactBook contents
]]]

And context menu is working fine: it asks new name and table updates after submit.

!!!! Enabling shorcuts

Now we can enable shortcuts on the presenter. Just implement method ==ensureKeyBindingsFor:==:

[[[
ContactBookPresenter >> ensureKeyBindingsFor: aWidget
	self enableCommanderShortcutsIn: aWidget
]]]

Then reopen window, select contact and press cmd+r (on Mac). You will get same UI dialog as in case of menu.

!!! Adding more commands

At the end try create few other commands. With ready infrastructure in presenter the creation of new commands is very straightforward process:

!!!! 1. Command to modify contact phone number:

[[[
CmdCommand subclass: #ChangePhoneCommand
	instanceVariableNames: 'contact newPhone'
	classVariableNames: ''
	package: 'Commander-Examples'

ChangePhoneCommand >> execute
	contact phone: newPhone

ChangePhoneCommand >> prepareFullExecutionInContext: aToolContext
	super prepareFullExecutionInContext: aToolContext.

	contact := aToolContext selectedContact.

	newPhone := UIManager default
		request: 'New phone for the contact'
		initialAnswer: contact phone
		title: 'Set new phone for contact'.
	newPhone isEmptyOrNil | (newPhone = contact name) ifTrue: [ ^ CmdCommandAborted signal ]

ChangePhoneCommand >> applyResultInContext: aToolContext
	super applyResultInContext: aToolContext.

	aToolContext updateView

ChangePhoneCommand >> defaultMenuItemName
	^'New phone'
]]]

Activation methods:

[[[
ChangePhoneCommand class >> contextMenuActivation
	<classAnnotation>
	^CmdContextMenuCommandActivation byRootGroupItemFor: CmdContactBookPresenter

ChangePhoneCommand class >> shortcutActivation
	<classAnnotation>
	^CmdShortcutCommandActivation by: $p meta for: CmdContactBookPresenter
]]]

!!!! 2. Command to remove contact:

[[[
CmdCommand subclass: #RemoveContactCommand
	instanceVariableNames: 'contactBook contact'
	classVariableNames: ''
	package: 'Commander-Examples'

RemoveContactCommand >> execute
	contactBook removeContact: contact

RemoveContactCommand >> prepareFullExecutionInContext: aToolContext
	super prepareFullExecutionInContext: aToolContext.

	contactBook := aToolContext selectedContactBook.
	contact := aToolContext selectedContact.

RemoveContactCommand >> applyResultInContext: aToolContext
	super applyResultInContext: aToolContext.

	aToolContext updateView

RemoveContactCommand >> defaultMenuItemName
	^'Remove'
]]]

Here we extend our requirement to the context: to be able remove a contact we also need a contact book instance where we should perform deletion.

And we implement new method ==selectedContactBook== in the presenter:

[[[
ContactBookPresenter >> selectedContactBook
	^contactBook
]]]

Notice that in ==prepare== method we could call accessor ==contactBook== from aToolContext. But it leads to bad reusability of command because the name ==contactBook== means single book instance while other applications can provide multiple of them in same way like our current presenter provides multiple contacts.
So in sense of tool context it is better to call ==selectedContactBook== because it is better reflects abstract requirement of information: we expect that there will be selected book in the context of command activation.

Activation methods:

[[[
RemoveContactCommand class >> contextMenuActivation
	<classAnnotation>
	^CmdContextMenuCommandActivation byRootGroupItemFor: CmdContactBookPresenter

RemoveContactCommand class >> shortcutActivation
	<classAnnotation>
	^CmdShortcutCommandActivation by: $x meta for: CmdContactBookPresenter
]]]

!!!! 3. Command to add contact:

[[[
CmdCommand subclass: #AddContactCommand
	instanceVariableNames: 'contactBook contact'
	classVariableNames: ''
	package: 'Commander-Examples'

AddContactCommand >> execute
	contactBook addContact: contact

AddContactCommand >> prepareFullExecutionInContext: aToolContext
	| nameAndPhone pair |
	super prepareFullExecutionInContext: aToolContext.
	contactBook := aToolContext selectedContactBook.

	nameAndPhone := UIManager default
		request: 'Enter new contact name and phone (split by comma)'
		initialAnswer: 'name;phone'
		title: 'Create new contact'.
	nameAndPhone isEmptyOrNil ifTrue: [ ^ CmdCommandAborted signal ].
	pair := (';' split: nameAndPhone).
	contact := CmdContact named: pair first phone: pair last

AddContactCommand >> applyResultInContext: aToolContext
	super applyResultInContext: aToolContext.

	aToolContext updateView

AddContactCommand >> defaultMenuItemName
	^'New contact'
]]]

In preparation we request user for both name and phone number in simple comma separated form.

Activation methods:

[[[
AddContactCommand class >> contextMenuActivation
	<classAnnotation>
	^CmdContextMenuCommandActivation byRootGroupItemFor: CmdContactBookPresenter

AddContactCommand class >> shortcutActivation
	<classAnnotation>
	^CmdShortcutCommandActivation by: $n meta for: CmdContactBookPresenter
]]]

%+Family book menu>file://figures/FamilyMenuDefault.png+

It is up to you to maintain commands in good shape. For example commands here use same state ==contact== and duplicate method ==applyResultInContext:==. You can refactor it with your taste, extract common superclass and so on. (In the example package they are not refactored)

@@todo Stef here

!!! Extra command conditions

Now there will be error if you will try rename contact when no item is selected in the table.

Normally menu should not include items in such cases or they should be disabled.

To define runtime condition where command can be activated you need class side method ==canBeExecutedInContext:==:

[[[
RenameContactCommand class>>canBeExecutedInContext: aToolContext
	^aToolContext isContactSelected
]]]

(As default implementation any command can be executed in any context. So by default this message returns true)

And presenter should implement ==isContactSelected==:
[[[
ContactBookPresenter>>isContactSelected
	^table selectedItem notNil
]]]

!!! Context menu tuning

We already redefine menu item name in the method ==defaultMenuItemName==.

There are other settings:
- order of item in menu
- hierarchical menu
- separator between items
- menu icon
- custom menu item

!!!! Menu item order

In our example 'Rename' item was placed at the end of menu because by default items are sorted in alphabetical order.

To change the order the activation strategy should be created with extra parameter:

[[[
RenameContactCommand >> contextMenuActivation
	<classAnnotation>
	^CmdContextMenuCommandActivation byRootGroupItemOrder: 1 for: CmdContactBookPresenter
]]]

The lesser order value puts item to the top of menu list.

%+Context menu reordered>file://Figures/FamilyMenuRenameFirst.png+

!!!! Hierarchical menu

Commander allows create hierarchical menu using reified menu groups.
Imaging that you want put 'New phone' command under 'Phone operations' submenu.

First you need new subclass of CmdMenuGroup:

[[[
CmdMenuGroup subclass: #PhoneMenuGroup
	instanceVariableNames: ''
	classVariableNames: ''
	package: 'Commander-Examples'
]]]

And then add this class as extra parameter to the menu activation strategy:

[[[
ChangePhoneCommand >> contextMenuActivation
	<classAnnotation>
	^CmdContextMenuCommandActivation byItemOf: PhoneMenuGroup for: CmdContactBookPresenter
]]]

It will moves ChangePhoneCommand to extra submenu with name 'PhoneMenuGroup'. You can specify better name as in case of commands:

[[[
PhoneMenuGroup>>name
	^'Phone operations'
]]]

%+Submenu>file://Figures/FamilyMenuPhoneGroup.png+

If you need deep menu structure just define class side method ==parent== in your very child group:

[[[
DeepChildrenMenuGroup class>>parentGroup
	^PhoneMenuGroup
]]]

!!!! Menu separators

Commander uses separators to represent inlined menu groups.

If group is marked as inlined then all items are added to the parent group with required separator lines. To inline group you need override method ==isInlined==:

[[[
PhoneMenuGroup>>isInlined
	^true
]]]

%+Inlined menu>file://Figures/FamilyMenuInlined.png+

!!!! Menu icons

To specify icon the command should implement the instance side method ==setUpIconForMenuItem:==. For example we can add icon to the ==RemoveContactCommand==:

[[[
RemoveContactCommand>>setUpIconForMenuItem: aMenuItemMorph
	self setUpIconNamed: #removeIcon forMenuItem: aMenuItemMorph
]]]

%+Iconic menu item>file://Figures/FamilyMenuWithIcon.png+

As you can see it is not primitive ==icon== method which returns the icon instance.

The idea is to be able create dynamic and complex icons based on command state.

!!!! Custom menu items

When Commander builds context menu it asks command itself to build view (a morph) for menu item.
By default CmdCommand builds standard menu item morph but subclasses can redefine it to be represented specifically.

For example we can represent item with different color:

[[[
RenameContactCommand>>fillContextMenu: aMenu using: aCommandActivator
	| item |
	item := aMenu
		add: aCommandActivator menuItemName
		target: aCommandActivator
		selector: #executeCommand.

	item color: Color red
]]]

%+Custom menu item>file://Figures/FamilyMenuWithCustomItem.png+

You can notice here that command can be represented by multiple menu items. ==fillContextMenu== method provides full control under menu.
For example you can generate multiple command copies with some specific parameters. It allows dynamic menus which can be needed in some cases.

Also same kind of flexibility is available for groups. Look at default implementation of menu group for example:

[[[
CmdMenuGroup>>buildContextSubMenuIn: aMenu
	| submenu |
	submenu := UIManager default newMenuIn: aMenu defaultTarget for: aMenu defaultTarget.

	contents do: [ :each | each buildContextMenu: submenu ].

	submenu hasItems ifTrue: [ aMenu add: self name subMenu: submenu]
]]]

You can redefine it in your own group to build very special menus.

!!! Drag and drop example

In this section you will learn how activate commands using drag and drop operation.

During drag and drop operation user grabs some element from one widget and drops it on another widget. And the application handles special event which brings together the information about source and target elements.

Commander uses drag and drop to prepare commands in two steps:

1) When user starts drag some element from the widget Commander finds and prepares commands in the context of this widget and dragged element. Then found commands are used as passengers of drag operation.
2) When user drops element to the widget Commander filters current dragged commands using declared drop context and the context of target widget. At the end it executes found command.

If Commander does not find any commands in drag context the drag and drop operation is not started.

If Commander does not find any command suitable for drop context it forbids drop on current widget. UI gives visual feedback for the places where drop can be executed. And when drop is forbidden you will not see this effect.

If Commander finds multiple commands suitable for the drop context it will show menu to request user to choose concrete action.

Now we can annotate existing command ==AddContactCommand== with drag and drop activation:

[[[
AddContactCommand class >> dragAndDropActivation
	<classAnnotation>
	^CmdDragAndDropCommandActivation for: ContactBookPresenter toDropIn: ContactBookPresenter
]]]

First parameter describes context where drag can be started. And last parameter is a context where drop can be executed. In example both are the same presenter which enables drag and drop contacts between contact books.

As in example with context menu and shortcuts we also need activate Commander based drag and drop in our presenter. It should be enabled in ==initializeWidgets== method:

[[[
ContactBookPresenter >> initializeWidgets
	table := self newList.
	table numberOfColumns: 2 withWidths: #(250 250).
	table displayBlock: [ :contact | {contact name. contact phone} ].
	table items: contactBook contents.
	table enableCommanderMenuBy: self.

	table enableCommanderDragAndDropBy: self.
]]]

And to prepare command instance in two steps we need implement to methods:

[[[
AddContactCommand >> prepareExecutionInDragContext: aToolContext
	super prepareExecutionInDragContext: aToolContext.

	contact := aToolContext selectedContact
]]]

==prepareExecutionInDragContext== extracts information from the context of drag operation. In that case we decided that command can be only executed by drag of selected contact.

[[[
AddContactCommand >> prepareExecutionInDropContext: aToolContext
	super prepareExecutionInDropContext: aToolContext.

	contactBook := aToolContext selectedContactBook
]]]

And in second method ==prepareExecutionInDropContext== we extract the rest information from the context where the drop was executed. In that case we decided that command can be only executed by drop into the contact book.

Now drag and drop should work. To play with it let's create another book of coworkers on the class side of ==ContactBook==:

[[[
ContactBook class >> coworkers
	^coworkers ifNil: [
		coworkers := self new
			add: 'Stef' phone: '112 378';
			add: 'Pavel' phone: '898 678';
			add: 'Marcus' phone: '444 888';
			yourself]
]]]

And we need extra example method on the class side of presenter:

[[[
ContactBookPresenter class>>coworkersExample
	<example>
	^ (self on: CmdContactBook coworkers) openWithSpec
]]]

Now open two windows: ==coworkersExample== and ==familyExample==. And drag and drop contacts between them.

You can notice that you are able dublicate contact when it already exists in the target book. Also dublication happens when you drop contact in same source book.

To prevent it you can specify extra runtime condition ==canBeExecutedInDropContext:== as in case of full command execution:

[[[
AddContactCommand>>canBeExecutedInDropContext: aToolContext
	^aToolContext isContactBookSelected
		and: [ (aToolContext selectedContactBook includesContact: contact) not ]
]]]

Notice that this method is on instance side and it can use the data which was extracted from drag context. Here we check that ==contact== not exists in the target contact book.

When Commander collects commands at the time of drag operation it prepares them using method ==prepareExecutionInDragContext==. So at the time of drop operation all found commands are initialized with information from drag context.

Also notice that here we add extra check ==isContactBookSelected==. We can omit it because in our example contact book always exists in presenter. But for reusability purpose we should implement this method independently from our knowledge about existing context. Imaging that we will have another presenter with table of existing books and we will need to drag contacts to it items. In that case it will be required to check that book is actually selected. Instead we will get error when table has no selection.

So at the end our presenter do not accept drops of contacts which aready exist in the book.

!!! Complex contexts

In examples command context was always represented by presenter itself. It is default behavior of Spec integration with Commander.

Problem with this approach is that presenter always bound to its own current state. It is fine for simple cases. But for example you are not able to activate our commands in context of contact which is not selected in the table. Following example demonstrates problem in live:

You can notice that dropping contact into the book always adds it to the end of list. But drop operation is always points to some element in the table. So we can support drop to the particular position.

For this we need extend ==AddContactCommand== with ==afterContact== variable after which we will add new contact:

[[[
CmdCommand subclass: #AddContactCommand
	instanceVariableNames: 'contactBook afterContact contact'
	classVariableNames: ''
	package: 'Commander-Examples'
]]]

And we should modify execution method to take into account new logic. Also we need extra method for ==ContactBook==:

[[[
AddContactCommand>>execute
	afterContact
		ifNil: [ contactBook addContact: contact]
		ifNotNil: [ contactBook addContact: contact after: afterContact ]

ContactBook>>addContact: newContact after: contactBefore
	contents add: newContact after: contactBefore
]]]

So if ==afterContact== is not specified the new contact will be added to the end of list.

We can support this logic in full execution scenario where command is activated from menu or by shortcut:

[[[
AddContactCommand>>prepareFullExecutionInContext: aToolContext
	| nameAndPhone pair |
	super prepareFullExecutionInContext: aToolContext.

	contactBook := aToolContext selectedContactBook.
	aToolContext isContactSelected ifTrue: [
		afterContact := aToolContext selectedContact].

	nameAndPhone := UIManager default
		request: 'Enter new contact name and phone (split by comma)'
		initialAnswer: 'name,phone'
		title: 'Create new contact'.
	nameAndPhone isEmptyOrNil ifTrue: [ ^ CmdCommandAborted signal ].
	pair := (',' split: nameAndPhone).
	contact := CmdContact named: pair first phone: pair last
]]]

New contact will be added after selected contact in the context of target presenter (aToolContext is a presenter instance for now).

You can test new logic by creation new contacts from menu or by shortcuts.

For drag and drop case we should also modify ==prepareExecutionInDropContext:==:

[[[
AddContactCommand>>prepareExecutionInDropContext: aToolContext
	super prepareExecutionInDropContext: aToolContext.

	contactBook := aToolContext selectedContactBook.
	aToolContext isContactSelected ifTrue: [
		afterContact := aToolContext selectedContact].
]]]

Now if you would play with drag and drop you will notice that it works in not expected way: new contact is always added into the target book after selected item instead of the item where drop is executed.

The problem is that the selection of the drop target is the static state of the target book presenter. It could not be different depending on the actual place of drop operation.

And here we finally should introduce the real context which will incapsulate selected contact independently of presenter. This example shows that the simple case where presenter is its own context has limitations.

Contexts are subclasses of ==CmdToolContext== and they should include all information to describe the state of underlying tool. In our case it will include ==selectedContacts== variable:

[[[
CmdToolContext subclass: #ContactBookPresenterContext
	instanceVariableNames: 'selectedContacts'
	classVariableNames: ''
	package: 'Commander-Examples'
]]]

For Spec support Commander passes multiple items to request context from presenter. So we should use contact collection instead of single item. And generally it is more common case. Selection can include multiple items.

In addition context should support all methods which are required by commands:

[[[
ContactBookPresenterContext>>selectedContact
	^selectedContacts last "last selected item"

ContactBookPresenterContext>>isContactSelected
		^selectedContacts notEmpty

ContactBookPresenterContext>>selectedContactBook
		^tool contactBook

ContactBookPresenterContext>>isContactBookSelected
		^true
]]]

Notice that base class of context ==CmdToolContext== provides ==tool== variable which is presenter instance in our case.

Also to implement ==selectedContactBook== we do not need to call ==selectedContactBook== from presenter. It was the message to represent presenter as own context. So ==selectedContactBook== can be removed from presenter together with ==isContactBookSelected== method because they are now defined by reified context class.
(==ContactBookPresenterContext== tightly bound to the ==ContactBookPresenter==. So it knows that contact book is mondatory property of presenter and it can call accessor directly).

To inject new context to our presenter we should override two methods:

[[[
ContactBookPresenter>>createCommandContextForSelection: selectedContacts
	^ContactBookPresenterContext for: self of: selectedContacts

ContactBookPresenter>>createCommandContext
	^self createCommandContextForSelection: table selectedItems
]]]

First method creates concrete context instance with selected items. And last method provides default selection (from the table) to the context.

And the last important details is needed:

When we introduced new context the way how Commander searches commands is changed. By default (for Spec) Commander finds all commands which are annotated with given presenter class (using isKindOf: check for this). But in general case with reified context Commander scans commands using class of context instead of class of tool.

In general case activation strategies should be attached to commands using class of context instead of tool. The reason why it is implemented like this is that single application/presenter can provide multiple contexts depending on child component which triggers menu, shortcuts and so on. In general case single presenter can have many different menus. For example we can create presenter with two tables. And each table will provide own menu and shortcuts.

So if we will do nothing our example will loose all menus and shortcuts. There is two ways to fix it:

1. Modify all command activations to use context class ==ContactBookPresenterContext== instead of presenter class ==ContactBookPresenter==.

This approach would be required if we would extend our presenter with extra component which will provide separate set of commands. But in our example we do not need it.

2. Adopt our context to be simple single context of presenter.

In case when presenter provides single set of commands we can just mark the context as simple. It is preferred solution for simple presenters because in that case internal context implementation will be hidden from users. All commands will mention only presenter class in annotations.

So in simple context the tool (presenter) is actual user of commands. And it should redefine the method ==representsSimpleAnnotationUser:== by testing that the tool is a kind of given class.

In our example we follow this approach:

[[[
ContactBookPresenterContext>>representsSimpleAnnotationUser: aToolClass
	^tool isKindOf: aToolClass
]]]

Now if you will try again drag and drop you will see that drop inserts contact after element where drop is executed.

More details on this part:
==representsSimpleAnnotationUser== method is part of ClassAnnotation library. When you create annotation using class as context the annotation will be visible only for instances of this class hierarchy. Annotation will use simple ==isKindOf:== check to detect that given user can find it. But user can redefine this logic using this method.

In case of Commander the user of annotation is a tool context. But there is ==CmdSimpleToolContext== which is default for Spec presenters. It redefines this logic in the way that the tool becomes actual user of annotation. It simply checks that the tool is a kind of declared user class. But notice that ==CmdSimpleToolContext== should not be used as superclass for new contexts because it overrides other logic of base ==CmdToolContext== class.

So simple reified contexts should redefine this method in same way as ==CmdSimpleToolContext==. And it is what we did in this example.

!!! Advanced command activation

All commands in our examples define standard activation methods:

- ==prepareFullExecutionInContext:==
- ==canBeExecutedInContext:==
- ==prepareExecutionInDragContext:==
- ==prepareExecutionInDropContext:==
- ==canBeExecutedInDropContext:==

For some reason it can be impossible to reuse these methods in specific applications.

In that case such application should provide special contexts which will override activation logic and send its own set of messages to command.

For example:

[[[
SpecialContextA >> allowsExecutionOf: aCommand
	 ^aCommand canBeExecutedInSpecialContextA: self

SpecialContextA >> prepareFullExecutionOf: aCommand
  aCommand prepareFullExecutionInSpecialContextA: self

SpecialContextA >> applyResultOf: aCommand
  aCommand applyResultInSpecialContextA: self

SpecialContextA >> allowsDropExecutionOf: aCommand
  ^aCommand canBeExecutedInSpecialDropContextA: self

SpecialContextA >> prepareDragActivationOf: aCommand
	aCommand prepareExecutionInSpecialDragContextA: self

SpecialContextA >> prepareDropExecutionOf: aCommand
	aCommand prepareExecutionInSpecialDropContextA: self
]]]

Then application can extend base ==CmdCommand== class with default implementation like:

[[[
CmdCommand >> prepareFullExecutionInSpecialContextA: aSpecialContextA
  self prepareFullExecutionInContext: aSpecialContextA
]]]

And only particular commands will implement them specifically:

[[[
SomeCommand >> prepareFullExecutionInSpecialContextA: aSpecialContextA
  "special logic to prepare command for execution"
]]]

!!! Command lookup and execution

Commands are annotated with activation strategy using context where they can be used.

You can inspect all strategies (and their commands) using ClassAnnotation API:

[[[
CmdShortcutCommandActivation registeredInstances
]]]

It will return all shortcuts in the system.

[[[
CmdContextMenuCommandActivation registeredInstances
]]]

It will return all menu items in the system.

To filter commands for concrete application you need context instance. For our presenter it would be:

[[[
context := (CmdContactBookPresenter on: CmdContactBook family) createCommandContext
]]]

Then you can query all commands suitable for this context:

[[[
CmdShortcutCommandActivation visibleInstancesInContext: context
]]]

It will return 4 shortcuts according to the example.

And you can filter them to see only active commands:

[[[
CmdShortcutCommandActivation activeInstancesInContext: context
]]]

It will return single activation because only command exist which can be executed without selected item (by default the table selection is empty).

To execute collected commands you should create the command activator instance:

[[[
activator := (CmdShortcutCommandActivation activeInstancesInContext: context) first newActivatorFor: context
]]]

It incapsulates given context and performs all required logic to prepare command before execution and to apply result after execution:

[[[
activator executeCommand
]]]

There is suitable method to enumerate all commands and execute them with prepared activator instance:

[[[
CmdShortcutCommandActivation
	activateAllInContext: context
	by: [:activator | activator executeCommand]
]]]

!!! Menu activation

There are a lot of different types of menu: context menu, toolbar, morphic halo menu, etc.
All of them are very similar: they show set of items to the user and when user selects one the action associated with item is evaluated. The main difference between them is the way how they are represented to user and where they are shown in application.

The commander library allows the building of different kind of menus based on commands and first class groups. Concrete type of menu is represented by concrete type of activation strategy, subclass of ==CmdMenuCommandActivationStrategy==. The simplest example is ==CmdContextMenuCommandActivation== which we used to define context menu in our presenter example. Now it is only available in Commander. For other examples look at Calypso browser.

So users annotate commands with menu activations and Commander build menus using them:

[[[
YourCommand >> yourAppMenuActivation
  <classAnnotation>
  ^ConcreteMenuCommandActivation byItemOf: YourAppChildMenuGroup for: YourAppContext
]]]

Activation provides information about name, group and position of command inside menu:
- ==menuItemName== (by default it is retrieved from command instance by sending the message ==defaultMenuItemName==).
- ==menuItemOrder== (higher value pushes command to the end of menu)
- ==menuGroup== (root by default)

To set up these properties there are few instance creation methods:
- ==byRootGroupItemFor: YourAppContext==
- ==byRootGroupItemOrder: aNumber for: YourAppContext==
- ==byItemOf: menuGroupClass for: YourAppContext==
- ==byItemOf: menuGroupClass order: aNumber for: YourAppContext==

The tree structure of menu is represented by ==CmdMenu== instance. It should be created with concrete class of menu activation. For example for context menu it would be:

[[[
menu := CmdMenu activatedBy: CmdContextMenuCommandActivation
]]]

And then you can ask it to build items suitable for given context:

[[[
menu buildInContext: context
]]]

If your application provides multiple context you can call this method for all of them. In that case menu will include items from all widgets which provide these context.

For our example you can inspect menu instance after ==build== method. In inspector you will see ==rootGroup== variable where you can reach menu items and underlying commands.

When menu instance is ready you can ask it to create concrete menu object like Morphic context menu:

[[[
(menu buildContextMenuFor: World) openInWorld.
]]]

It is the way how implement new kind of menus. First, create new subclass of ==CmdMenuCommandActivationStrategy== and then extend ==CmdMenu== with new logic to build required menu object. You can extend ==CmdCommand== to represent item specifically for your type of menu.

Look for details at package ==Commander-Activators-ContextMenu== which implements classic context menu based on commands. For example ==CmdContextMenuCommandActivation== implements helper method to create context menu for single application context. So previous example with building menu can be simplified:

[[[
(CmdContextMenuCommandActivation buildMenuFor: World inContext: context) openInWorld
]]]

Context menu extends command with menu building method:

[[[
CmdCommand >> fillContextMenu: aMenu using: anActivator
]]]

It creates menu item morph and allows subclasses to define default label and icon:

- ==defaultMenuItemName==
- ==setUpIconForMenuItem: aMenuItemMorph==

Subclasses can override building method ==fillContextMenu:using:== to represent items differently. For example they can create menu item with check box or decorate them with special colors.

!!! Shortcut activation

As you saw in example section ==CmdShortcutCommandActivation== is used to annotated commands with shortcut:

[[[
RenameContactCommand class >> shortcutActivation
	<classAnnotation>
	^ CmdShortcutCommandActivation by: $r meta for: ContactBookPresenter
]]]

There are extra helper methods for standard shortcuts:
- ==renamingFor:==
- ==removalFor:==

So we can rewrite activation using:

[[[
RenameContactCommand class >> shortcutActivation
	<classAnnotation>
	^ CmdShortcutCommandActivation renamingFor: ContactBookPresenter
]]]

Notice that class annotations are cached and updated only when declaration method is changed. It means that your should manually update cache when you modify helper methods like ==renameingFor:==:

[[[
ClassAnnotation resetAll
]]]

On the low level of Morphic integration the application should define specific kmDispatcher for target morphs:

[[[
YourAppMorph >> kmDispatcher
  ^CmdKMDispatcher attachedTo: self
]]]

with supporting method:

[[[
YourAppMorph >> createCommandContext
  ^YourAppContext for: self
]]]

In example we were using high level Spec integration where we just enable Commander in target presenter:

[[[
ContactBookPresenter >> ensureKeyBindingsFor: aWidget
	self enableCommanderShortcutsIn: aWidget
]]]

So users do not need to think how morphs are connected in case of Spec.

Other libraries/frameworks can privide similar integration to simplify Commander usage.

!!! Conclusion
